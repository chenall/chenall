import win.ui;
import win.ui.menu;
import web.form; 
import web.json;
import inet;
import inet.http;
import web.mshtml;
import key;
import mouse;
import fsys.ini;
import tcc;
import fsys.file
import string.crypt;
import sqlite;
/*DSG{{*/
var winform = ..win.form( bottom=300;scroll=1;right=450;text="网盘自动签到 by chenall http://chenall.net";parent=...)
winform.add( 
checkbox={ bottom=22;text="显示WEB窗口";left=50;top=5;z=2;right=138;hide=1;cls="checkbox" };
button2={ bottom=296;right=63;left=5;top=263;z=13;text="签到记录";cls="button" };
static={ bottom=47;text="网盘:";left=9;right=41;top=28;z=4;transparent=1;cls="static" };
static2={ bottom=77;text="用户名:";left=6;right=49;top=60;z=6;transparent=1;cls="static" };
disk_type={ bottom=46;text="请选择网盘";left=41;top=26;right=140;z=3;
items={  };mode="dropdownlist";edge=1;cls="combobox" };
password={ bottom=103;right=142;left=53;top=84;z=9;tabstop=1;edge=1;cls="edit" };
username={ bottom=74;right=143;left=54;top=54;z=8;tabstop=1;edge=1;cls="edit" };
static3={ bottom=103;text="密码:";left=8;right=53;top=86;z=7;transparent=1;cls="static" };
button={ bottom=297;text="开始签到";left=71;top=264;z=5;right=145;cls="button" };
add_site={ bottom=133;text="添加";left=97;right=143;top=106;tabstop=1;z=10;cls="button" };
accounts={ bgcolor=16777215;bottom=257;right=146;left=7;cls="listview";top=139;vscroll=1;aw=1;gridLines=1;z=11;edge=1;fullRow=1 };
listbox={ ah=1;bgcolor=16777215;bottom=299;vscroll=1;right=450;border=1;left=151;top=6;msel=1;z=1;
items={  };hscroll=1;aw=1;cls="listbox" };
sina={ bottom=134;color=0;text="新浪微搏登录";left=7;top=107;transparent=1;right=91;z=12;cls="button" }
)
/*}}*/

/*DSG{{*/
var webform = win.form( bottom=400;parent=...;text="网盘自动签到 By chenall";exmode="none";min=false;scroll=1;mode="popup";right=600 )
webform.add(  )
/*}}*/


//创建web窗体
var wb = web.form( webform,
	,//可输入_UIFLAG_ 前缀的常量自定义外观
	,//可输入_DLCTL_ 前缀的常量以控制下载行为
	,//"USER AGENT"
	);
var sql = null;
var http = inet.http();
var mht = web.mshtml();
var main_exit = false; //主窗口已经关闭?如果关闭了就退出程序.
var acc_sel = 0;
var sina_user="";
var sina_pass="";
var sina_logined = false;
var	logfile = fsys.file("\签记记录.log","a+");
var sign_funcs;
var disk_type;

webform.onClose = function(hwnd,message,wParam,lParam){
	if (main_exit)//主窗口已经半闭.
		return;
	webform.show(false);
	winform.checkbox.checked = false;
    return false;//禁止关闭此窗口
}

winform.onClose = function(hwnd,message,wParam,lParam){
	main_exit = true;
    webform.close();
}

winform.button2.oncommand = function(id,event){
	//winform.msgbox( winform.button2.text );
	sign_log(null)	
}

winform.add_site.oncommand = function(id,event){
	if (winform.password.text == "" || winform.password.text == "" ||winform.disk_type.selIndex == "" )
		return;
	var user = winform.username.text;
	var pass = winform.password.text;
	var disk_type = winform.disk_type.selIndex;
	var str = sign_funcs[disk_type](user,pass,-1);
	if (str == null)
	{
		winform.msgbox("添加帐号失败!");
		return;
	}
	if (str[1] == 1)
		pass = str[2];
	var id = ins_sql(disk_type,user,pass,str[2]);
	winform.accounts.addItem({tostring(id);user+"("+winform.disk_type.selText+")"});
	winform.username.text = "";
	winform.password.text = "";
}

winform.sina.oncommand = function(id,event){
	//winform.msgbox( winform.sina.text );
	if (winform.username.text != "" && winform.password.text != "")
	{
		sina_user = winform.username.text;
		sina_pass = winform.password.text;
		var key = sina_login(sina_user,sina_pass,-1);
		if (key == null)
			return;
		sql = sqlite("\config.db");
		sql.exec("update config set user='"+sina_user+"',pass='"+password_enc("@"+sina_pass)+"',key='"+key+"' where id=0");
		sql.close()
		sina_pass = key;
		winform.username.text = "";
		winform.password.text = "";
	}
	if (sina_user == "" || sina_pass == "")
	{
		winform.msgbox("请在用户名和密码输入框中输入新浪微搏帐号和密码(只需一次)","未设置新浪微搏帐号");
		return;
	}
	if (sina_logined == false && sina_login(sina_user,sina_pass) == false)
	{
		return null; 
	}
	id = sign_funcs[winform.disk_type.selIndex]("oauth","sina",-1);
	if (id == null)
		return;
	id = ins_sql(winform.disk_type.selIndex,"oauth","sina",id[2]);
	winform.accounts.addItem({tostring(id);"OA:sina("+winform.disk_type.selText+")"});
}

ins_sql = function(t,s,p,k="null"){
	sql = sqlite("\config.db");
	if (k != "null")
		k = "'"+k+"'";
	p = password_enc("@"+p);
	var id = sql.stepQuery("select id+1 as id from config a where not exists(select 1 from config where id=a.id+1) order by id limit 1")
	id = id['id']
	sql.exec("insert into config values("+id+","+t+",'"+s+"','"+p+"',"+k+")")
	sql.close();
	return id;
}

sign_log = function(item){
	sql = sqlite("\config.db")
	var logs = "SELECT * FROM sign";
	var string = "签到记录";
	if (item != null)
	{
		logs +=  " where did="+winform.accounts.items[item];
		string += "--"+winform.accounts.getItemText(item,2);
	}
	else {
		string = "签到记录"
		logs += ",disk_type where did=(select id from config where tid=type) order by date";
	}
	logs = sql.getTable(logs);
	sql.close();
	string += '<table style="bordercolor=\"#998877\" border=\"1\"">'
	for (k,v in logs)
	{
		string += "<tr>"
		if (item == null)
			string += "<td>"+v['name']+"</td>"
		string += "<td>"+v['date']+"</td><td>"+v['msg']+"</td></tr>";
	}
	string += "</table>"
	wb.write(string);
	webform.show(true);
	sql.close();
}

sign_log_add = function(id,msg){
	sql = sqlite("\config.db")
	sql.exec("insert into sign values("+id+",date('now','localtime'),'"+msg+"')")
	sql.close();
}


del_sql=function(id){
	sql = sqlite("\config.db");
	sql.exec("delete from config where ID="+id+";delete from sign where did="+id);
	sql.close();
}

winform.popmenu = win.ui.popmenu(winform);//创建弹出菜单
winform.popmenu.add('删除',function(id){
	var id = winform.accounts.selIndex;
	del_sql(winform.accounts.items[id]);
	winform.accounts.delItem(id);
});
winform.popmenu.add('签到记录',function(id){
	var id = winform.accounts.selIndex;
	sign_log(id);
});


winform.accounts.onnotify = function(id,code,ptr){ 
	var nm = winform.accounts.getNotifyMessage(code,ptr);
	select(code) {
		case 0xFFFFFFFB{//NM_RCLICK  
	   		if(nm.iItem){
    		    var x,y = mouse.getPos()
    			winform.popmenu.popup(x,y,true);//弹出菜单
        	}
    	}
    	case 0xFFFFFFFD{/*_NM_DBLCLK*/
			sign_log(winform.accounts.items[nm.iItem]) 
    	}
    }
  
}

code_c = /****
	char *xorstr (char *msg)
	{   
   		unsigned char *p = msg+1;
   		while (*p)
   		{
   			*p^=*msg;
   			if (*p <= '\'') *p^='e';
   			++p;
   		}
		return msg+1; 
	} 
****/

vm = tcc( ); 
 
//编译C源码
vm.compile(code_c); 
 
//获取C函数
password_enc = vm.getCdecl("xorstr","string(string msg)")
password_dec = password_enc

lstbox_add = function(msg){
	if (msg = null)
		return;
	var tm = time();
	tm.format="%H:%M:%S ";
	msg = tostring(tm) + msg;
	
	winform.listbox.add(msg)
	logfile.write(msg+'\r\n');
}

ramdon_delay = function(){
	win.delay(math.random(1000,6000));
}

GetLink = function(title,iframe = null){
    for(k,ele in wb.eachAll("A",iframe)){
        if(string.find(ele.innerHTMl,title )){
          return ele;
        }
    }
    return null;
}

//115网盘自动签到
get_115 = function(account,passwd,flags=0){
	var ret = null;
	lstbox_add("登录115网盘: " + account);
	var str=null;
	if (flags == 1 || account = "oauth")
	{
		str = http.get("https://passport.115.com/?ct=open_login&t="+passwd);
	}
	else {
		str = http.post("https://passport.115.com/?ac=login","login[account]="+account+"&login[passwd]="+passwd);
		str = string.match(str,"error = \'([^']*)");
		if (str)
		{
			lstbox_add(string.fromto(str));
			return null;
		}
	}
	
	str = http.get("http://115.com/?ct=index&ac=home");
	if (flags == -1)//flags == -1测试登录。获取帐号信息；
	{
		str = string.match(str,"\<em>\(([^)]*)");
		return {2;str};
	}
	if (str == null)
	{
		lstbox_add("网络错误!目前无法访问");
		return null;
	}
	var regexExpression =  "[0-9a-f]{32}" ;
	strMatchResult = string.match(str, regexExpression );
	if (strMatchResult == null)
	{
		ret = "已签到"
		lstbox_add(ret);
	}
	else {
		str = http.get("http://115.com/?ct=ajax_user&ac=pick_space&token="+strMatchResult);
		var tab = web.json.parse(str);
		if (tab['state'])
		{
			lstbox_add("签到成功:+"+tab['picked']+"共:" + tab['total_size']);
			ret =  tab['picked'];
		}
		else
			lstbox_add("签到失败!");
	}
	http.get("https://passport.115.com/?ac=logout");
	return  ret;
}

//华为网盘自动签到
get_dbank = function(su,sp,flags=0){
	lstbox_add("登录华为网盘:" + su);
	var res = null;
	//登录
	if (flags == 1 || su = "oauth")//type = qqspace 或　sina
	{
		if (sina_logined == false && sina_login(sina_user,sina_pass) == false)
		{
			return null; 
		}
		res = http.get("http://www.dbank.com/app/web/weibo.php?action=getLoginUrl&oauth_type=sina");
		res = string.sub(res,2,-2);
		res = string.replace(res,"\\/","/");
		wb.go(res);
		wb.wait();
	}
	else {
		res = http.get("http://login.dbank.com/loginauth.php?nsp_app=48049")
		var nonce = string.match( res,'nonce[^>]*value=\"([^"]*)\"');
		var nsp_cid = string.match(res,'nsp_cid[^>]*value=\"([^"]*)\"')
		if (flags == -1)
		{
			sp = su + ":NSP Passport:" + sp;
			sp = string.lower(string.crypt.md5(sp));
		}
		res = sp + ":" + nonce;
		res = string.lower(string.crypt.md5(res));
		res = http.post("http://login.dbank.com/loginauth.php?nsp_app=48049","m=1&nonce="+nonce+"&nsp_cid="+nsp_cid+"&nsp_user="+su+"&response="+res)
		res = web.json.parse(res);
		if (res['retcode'] != 0)
		{
			lstbox_add("登录失败: " +  res['message']);
			return null;
		}
		lstbox_add("华为网盘登录成功!");
		if (flags == -1)
		{
			http.get("http://login.dbank.com/accounts/logout");//退出;
			return {1;sp};
		}
		sp = res['k'];
		http.get("http://login.dbank.com/loginauth.php?k="+sp);
		//{"retcode":"0000","retdesc":"用户签到成功","type":2,"space":37}
		//{"retcode":"0001","retdesc":"用户已签到"}
	}
	//36f2f83c44c3a29b98d64417dbf75fd3nsp_cb_jqjspnsp_fmtJSnsp_sid9uuu1w7uCr8hkua.WjouavD4xYuIsGMH2ZO4BnY-N.7VXSUfnsp_svcnsp.user.getAccountsnsp_ts1340702904908type1
	var Cookie = inet.getCookie("http://www.dbank.com");
	var nsp_sid = string.match(Cookie,"session=([^;]*)");
	var secret = string.match(Cookie,"secret=([^;]*)");
	var nsp_ts = tonumber(time());
	var nsp_key;
	if (flags == -1)
	{
		try{
			lstbox_add("获取用户帐号:")
			nsp_key = secret + "nsp_cb_jqjspnsp_fmtJSnsp_sid"+nsp_sid+"nsp_svcnsp.user.getAccountsnsp_ts"+nsp_ts+"type1";
			nsp_key = string.crypt.md5(nsp_key)
			//获取用户帐号信息
			res = http.get("http://api.dbank.com/rest.php?nsp_svc=nsp.user.getAccounts&type=1&nsp_sid="+ nsp_sid + "&nsp_ts="+nsp_ts+"&nsp_key="+nsp_key+"&nsp_fmt=JS&nsp_cb=_jqjsp");
			res = string.match(res,'"account":"(.*)"');
			lstbox_add(res);
		}
		catch(e){
			lstbox_add("出错了:"+Cookie);
			res = null;
		}
		if (res == null)
			return null;
		wb.go("http://login.dbank.com/accounts/logout");//退出;
		return {2;res};
	}
	nsp_key = secret + "anticache205nsp_sid"+nsp_sid+"nsp_svccom.dbank.signin.signinnsp_ts"+nsp_ts;
	nsp_key = string.crypt.md5(nsp_key);
	nsp_key = "http://api.dbank.com/rest.php?anticache=205&nsp_svc=com.dbank.signin.signin"+"&nsp_ts="+nsp_ts+"&nsp_sid="+nsp_sid+"&nsp_key="+nsp_key;
	lstbox_add("自动签到URL:"+nsp_key);
	var result = http.get(nsp_key);
	var ret = null
	try{
		res = web.json.parse(result);
		if (res['retcode'] == "0000"){
			ret = res['space'] + "MB"
			lstbox_add("签到成功获得空间"+ret);
		}
		elseif(res['retcode'] == "0001"){
			ret = "已签到"
			lstbox_add(string.fromto(res['retdesc']))
		}
	}
	catch(e){
		lstbox_add("出现错误以下是相关数据：")
		lstbox_add(result);
	}

  	return ret;
}


//快盘自动签到.

get_kuaipan = function(account,passwd,flags=0){
	lstbox_add("登录快盘:" + account);
	var str;
	if (flags == 1 || account = "oauth")
	{
		lstbox_add("使用新浪微搏登录");
		str = http.get("https://api.weibo.com/oauth2/authorize?client_id=366158720&response_type=code&redirect_uri=http%3A%2F%2Fwww.kuaipan.cn%2Fapi.php%3Fac%3Doauth%26op%3Dsina%26act%3Dcallback");
	}
	else {
		//提交POST数据，自动登录
		str = http.post("http://www.kuaipan.cn/index.php?ac=account&op=login", "username="+account+"&userpwd="+passwd);
	}
	if (str == null)
	{
		lstbox_add("网络错误!目前无法访问");
		return null;
	}
	wb.write(string.fromto(str));//错误检测.
	wb.wait();
	if (wb.getEle("loginform"))//出现错误
	{
		var errstr = "未知错误!"
		try{
			errstr = wb.getEle("erroInfo").innerText;
		}
		lstbox_add("错误: "+errstr);
		return null;
	}
	//http://www.kuaipan.cn/index.php?ac=home&op=userinfo 获取用户信息
	if (flags == -1)
	{
		str = web.json.parse(http.get("http://www.kuaipan.cn/index.php?ac=home&op=userinfo"));
		lstbox_add("用户名:"+str['xLive']['username'])
		lstbox_add("用户ID:"+str['xLive']['userId'])
		return {2;str['xLive']['username']}; 
	}
	//自动签到
	var tab = web.json.parse(http.get("http://www.kuaipan.cn/index.php?ac=common&op=usersign"));
	http.get("http://www.kuaipan.cn/account_logout.htm");//注销
	if (tab['state'] == -102)
	{
	//{"status": {"grade": 0, "maxsize": 100, "points": 18, "period": 0}, "rewardsize": 14, "monthtask": {"M900": 18, "invite": 0, "maxnormal": 900, "normal": 18}, "todaytask": {"count": {"sign": 1}}, "userid": "12842259", "increase": 9, "state": 1}
		lstbox_add("已签到");
		return "已签到";
	}
	lstbox_add("签到获得空间: "+tab['rewardsize']+" 积分: "+tab['status']['points']);
	return tab['rewardsize']+'MB';
}


//189天翼云存储
get_189 = function(account,passwd,flags=0){
	if (flags == 1 || account == "oauth")
	{
		lstbox_add("189云不支持新浪微搏登录!");
		return null;
	}
	var ret = null;
	lstbox_add("登录天翼云存储:" + account);
	http.get("http://cloud.189.cn/logout.action");
	str = http.get("http://cloud.189.cn/udb/udb_login.jsp");
	if (str == null)
	{
		lstbox_add("网络错误!目前无法访问");
		return null;
	}
	wb.write(string.fromto(str));
	try{
		var str = 'txtUserID='+account+ '&txtPwd='+passwd+'&ibtn_Login=&__VIEWSTATE='+inet.url.encode(wb.getEle("__VIEWSTATE").value)+'&__EVENTVALIDATION='+inet.url.encode(wb.getEle("__EVENTVALIDATION").value);
		http.post("http://sersh.passport.189.cn/Logon/UDBCommon/S/PassportLogin.aspx",str);
		if (flags == -1)
		{
			lstbox_add("获取用户信息:")
			str = http.post("http://cloud.189.cn/userInfoJson.action","")
			str = web.json.parse(str)
			lstbox_add("帐号: " + str['account'])
			lstbox_add("昵称: " + str['nickName'])
			ret = str['account'];
			return;
		}
		str = http.get("http://cloud.189.cn/userSign.action");//获取空间;
		var tab = web.json.parse(str);
		if (tab['isSign'])
		{
			ret = "已签到";
			lstbox_add(ret);
		}
		else 
		{
			ret = tab['netdiskBonus']+"MB";
			lstbox_add("签到获得空间:" + ret);
		}
	}
	catch(e){
		wb.write(string.fromto(str));
		lstbox_add("出现未知错误，请到以下网站留言 http://chenall.net");
	}
	http.get("http://cloud.189.cn/logout.action");//注销
	if (flags == -1)
		return {2;ret}
	return ret;
}

//新浪POST登录方法来源:　http://www.sad-pig.com/?p=278
sina_login_http = function(su,sp="",flags = 0){
	import string.base64;
	import string.conv;
	lstbox_add("登录新浪微搏:" + su);
	su=inet.url.encode(string.base64.encode(su));
	var str = http.get("http://login.sina.com.cn/sso/prelogin.php?entry=miniblog&callback=sinaSSOController.preloginCallBack&client=ssologin.js&user="+su);
	str = string.sub(str,36,-1);
	var tab = web.json.parse(str);
	sp = sp+tostring(tab['servertime'])+tab['nonce'];
	sp=string.crypt.sha1(sp);
	sp=string.conv.lower(sp);
	str = "url=http%3A%2F%2Fweibo.com%2Fajaxlogin.php%3Fcallback%3Dparent.sinaSSOController.feedBackUrlCallBack&returntype=META&ssosimplelogin=1&su="+su+'&service=miniblog&servertime='+tab['servertime']+"&nonce="+tab['nonce']+'&pwencode=wsse&sp='+sp;
	str = http.post("http://login.sina.com.cn/sso/login.php?client=ssologin.js",str);
	tab = string.match(str, "location.replace\(\'(.*)\'\)");
	if (tab == null)
	{
		lstbox_add("登录失败: 用户名或密码错误！");
		return null;
	}
	lstbox_add("登录成功!");
	if (flags == -1)
	{
		str = http.get(tab);
		lstbox_add("获取用户信息:");
		tab = string.match(str,"CallBack\((.*)\)");
		tab = web.json.parse(tab);
		tab = tab["userinfo"];
		lstbox_add("微号: " + tab['uniqueid'])
		lstbox_add("帐号: " + tab['userid'])
		lstbox_add("昵称: " + tab['displayname']);
		return tab['displayname'];
	}
	//parent.sinaSSOController.feedBackUrlCallBack({"result":true,"userinfo":{"uniqueid":"2840237540","userid":"chenall@163.com","displayname":"123456abcd866","userdomain":"?wvr=3.6&lf=reg"}});
	return tab;
}

sina_login = function(account,passwd,flags = 0){
	sina_logined ＝ false;
	if (flags == -1)
	{
		passwd = string.lower(string.crypt.sha1(passwd));
		passwd = string.lower(string.crypt.sha1(passwd));
	}
	var ret =  sina_login_http(account,passwd,flags);
	if (flags == -1)
	{
		if (ret == null)
			return null
		return passwd;
	}
	if (flags == 1)
		wb.go(ret);
	else
		http.get(ret);
	sina_logined = true;
	return true;
}

get_vdisk = function(account,passwd,flags=0){
	lstbox_add("新浪微盘: " + account);
	if (flags == -1)
	{
		if (account == "oauth")
			return {2;sina_user};
		return sina_login(account,passwd,flags);
	}
	var ret = null;
	var res = http.get("http://vdisk.weibo.com/index/vidskCpacity");
	//{"quota_used":"3.7M","quota_total":"21.0G","quota_left":"21.0G","quota_percent":1,"quota_percent_format":0}
	if (string.find(res,"errcode"))
	{
		lstbox_add("出错：可能是登录失败:" + res);
		return null;
	}
	res = web.json.parse(res);
	var total = res['quota_total'];
	lstbox_add("总共空间:"+res['quota_total']+" 已用:"+res['quota_used']+"可用:"+res['quota_left']);
	res = http.post("http://vdisk.weibo.com/task/checkIn","");
	res = web.json.parse(res);
	if (res['errcode'] > 1)
	{
		lstbox_add("签到失败： 未知错误");
		return null;
	}
	if (res['errcode'] == 1)
	{
	/*
	{"errcode":1,"msg":"\u5df2\u7ecf\u7b7e\u5230\u8fc7\u4e86","data":{"app_uid":"2840237540","time":1340238833,"size":179,"star":3,"sent_weibo_size":0,"date":"20120621","quota":2307}}
	{"errcode":1,"msg":"\u5df2\u7ecf\u7b7e\u5230\u8fc7\u4e86","data":{"id":"8465727","app_uid":"1082888172","time":"1340238068","size":"46","star":"1","sent_weibo":"0","sent_weibo_size":"50","date":"20120621","quota":"20593"}}
	*/
		ret = "签到失败: " + res['msg'];
		lstbox_add(ret);
		ret = res['data']['size'];
		res = res['data']['sent_weibo_size'];
		if (res != 0)//已签到，也已经发送微搏
		{
			ret = "已签到:" + ret + "MB,发微搏:" + tostring(res) + "MB" + " 总:"+total;
			lstbox_add(ret);
			return ret;
		}
	}
	else {
		ret = res['data'][1];
		res = res['data'][2];
	}
	res = string.fromto("msg=微盘签到获得空间"+tostring(ret)+"MB,好运指数"+res+"颗星,目前已有空间:"+total+", 更多信息请访问 http://chenall.net/post/netdisk-autosign/",0,65001);
	res = http.post("http://vdisk.weibo.com/task/checkInSendWeibo",res,"","http://vdisk.weibo.com")
	res = web.json.parse(res);
	if (res['errcode'] != 0)
	{
		lstbox_add("微盘签到并发送微搏失败");
		res['data'] = 0;
	}
	ret = "签到:"+tostring(ret)+"MB,发微搏:"+tostring(res['data'])+"MB"+" 总:"+total
	lstbox_add(ret);
	//http.get("http://weibo.com/logout.php");
	return ret;
}


get_taojinbi = function(account,passwd,flags=0){
	if (flags == 1 || account == "oauth")
	{
		lstbox_add("淘宝网不支持新浪微搏登录!");
		return null;
	}
	var ret = null;
	lstbox_add("登渌淘宝网:"+account);
	//登录
	var str = http.post("https://login.taobao.com/member/login.jhtml","TPL_redirect_url=http://taojinbi.taobao.com/&TPL_username="+account+"&TPL_password="+inet.url.encode(passwd));
	var errinfo = string.match(str,"<window.location>.?=");
	if (errinfo == null)
	{
		errinfo = string.match(str,"error.\>([^<]*)");
		lstbox_add("登录错误:" + errinfo);
		return null;
	}
	if (flags == -1)
	{
		http.get("http://login.taobao.com/member/logout.jhtml");//退出
		return {2;account};
	}
	wb.write(str);
	wb.wait();
	str = wb.waitEle("Jianghu_tb_token").childNodes(0).value
	str = http.post("http://taojinbi.taobao.com/home/grant_everyday_coin.htm","_tb_token_="+str);
	var tab = web.json.parse(str);
	if (tab['code'] == 1)
	{
		ret = "连领:"+tab['daysTomorrow']+"天,今日:"+tostring(tab['coinNew']-tab['coinOld'])+",目前有:"+tab['coinNew']+"淘金币"
		lstbox_add(ret);
	}
	else {
		lstbox_add("已领取:"+str);
		ret = "我的淘金币:"+tab['coinNew'];
	}
	http.get("http://login.taobao.com/member/logout.jhtml");//退出
	return ret;
	//http://taojinbi.taobao.com/home/grant_everyday_coin.htm?t=1339778460054&_tb_token_=5378e78e3b630
	//{"code":1,"coinOld":2634,"coinNew":2674,"daysTomorrow":20,"coinTomorrow":"40","auth":true,"isTake":"false","takeAmount":"","friendNum":"","switcher":"true"}
	//{"code":2,"coinOld":2674,"coinNew":2674,"daysTomorrow":1,"coinTomorrow":"10","auth":true,"isTake":"","takeAmount":"","friendNum":"0","switcher":"true"}
}


auto_sign = function(){
	var ret = "0";
	var su,sp,key,st,flag
	lstbox_add("开始自动签到" + tostring(time()));
	sql = sqlite("\config.db")
	tab = sql.getTable("SELECT * from config where type > 0 and id not in (select did from sign where date=date('now','localtime'))");
	sql.close();
	for (k,v in tab)
	{
		su = v['user'];
		sp = password_dec( '@' + v['pass']);
		key = v['key'];
		st = v['type'];
		flag = 0;
		if (su == "oauth")
		{
			flag = 1;
			su = "OA:"+sp+" [" + key + ']';
			if (sina_logined == false && sina_login(sina_user,sina_pass) == false)
			{
				lstbox_add("关联新浪微搏登录失败");
				continue; 
			}
		}
		cur_account = disk_type[st] + ":" + su;
		ret = sign_funcs[st](su,sp,flag);
		if (ret != null)
		{
			sign_log_add(v["ID"],ret);
		}
	}
	lstbox_add("自动签到结束" + tostring(time()));
	logfile.close();
}

winform.button.oncommand = function(id,event){
	return auto_sign();
}


//winform.msgbox(tonumber(time()));
wb.go("http://2345.com/?k200321")
wb.noScriptErr=true;
winform.disk_type.selIndex = 1;
winform.accounts.insertColumn("ID",25);
winform.accounts.insertColumn("帐号",-1);
winform.accounts.insertColumn("签到",30);
winform.show();

sql = sqlite("\config.db");
if (!sql.existsTable("config"))
{
	sql.exec("CREATE TABLE config(
		ID INTEGER PRIMARY KEY AUTOINCREMENT,
		type INTEGER NOT NULL,
		user TEXT NOT NULL,
		pass TEXT NOT NULL,
		key TEXT,
		UNIQUE (ID) 
		);"
	)
	sql.exec("insert into config values (0,0,'','',null)");
}

if (!sql.existsTable("sign"))
{
	sql.exec("CREATE TABLE sign(
		did INTEGER,
		date TEXT NOT NULL,
		msg TEXT
		);"
	)
}

if (!sql.existsTable("disk_type"))
{
	sql.exec("CREATE TABLE disk_type(
		tid INTEGER,
		name TEXT NOT NULL
		);"
	)
	sql.exec("insert into disk_type values (1,'华为网盘')");
	sql.exec("insert into disk_type values (2,'金山快盘')");
	sql.exec("insert into disk_type values (3,'新浪微盘')");
	sql.exec("insert into disk_type values (4,'115 网盘')");
	sql.exec("insert into disk_type values (5,'天翼网盘')");
	sql.exec("insert into disk_type values (6,'淘宝淘金币')");
}

tab = sql.getTable("select config.*,ifnull(did,0)>0 as st from config left join sign on did=id and date=date('now','localtime') group by id")
disk_type = sql.getTable("SELECT name FROM disk_type order by tid");
sql.close();
for (k,v in disk_type)
{
	winform.disk_type.add(v['name']);
}

disk_type = winform.disk_type.items;
winform.disk_type.selIndex = 1;

for (k,v in tab)
{
	if (v['type'] == 0)
	{
		sina_user = v['user'];
		sina_pass = v['key'];
		continue;
	}
	var su = v['user'];
	var sp = password_dec('@'+ v['pass']);
	var st = null;
	if (su == "oauth")
	{
		su = "OA:"+sp;
	}
	if (v['st'] != 0)
	{
		st = '√ '
	}
	winform.accounts.addItem({tostring(v['ID']);su+"(" + disk_type[v['type']] + ")";st})
}

sign_funcs = {get_dbank;get_kuaipan;get_vdisk;get_115;get_189;get_taojinbi};


inet.clearCookie();
inet.clearSesseion();
if (_CMDLINE = "auto")
{
	win.delay(3000)
	auto_sign();
	win.delay(3000);
	winform.close();
}

win.loopMessage();
